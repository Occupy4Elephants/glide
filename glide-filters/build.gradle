plugins {
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.6'
    id 'net.researchgate.release' version '2.3.5'
}

//version = project.glideFiltersVersion
description = "Filters for glide apps"

dependencies {
    compile 'javax.servlet:servlet-api:2.5'
    compile "com.google.appengine:appengine-api-1.0-sdk:$appengineVersion"
    compile "com.google.appengine:appengine-api-labs:$appengineVersion"

    testCompile "org.codehaus.groovy:groovy-all:${project.groovyVersion}"
    testCompile "org.spockframework:spock-core:${spockVersion}", {
        exclude group: 'org.codehaus.groovy'
    }

}

final EMPTY_LIST = Collections.unmodifiableList([])

sourceSets {
    main.java.srcDirs = ['src']
    test.groovy.srcDirs = ['test']

    main.resources.srcDirs = test.resources.srcDirs = main.groovy.srcDirs = test.java.srcDirs = EMPTY_LIST
}

publishing {
    publications {
        filters(MavenPublication) {
            from components.java
        }
    }
}

// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = project.properties.get "bintrayUser"
    key = project.properties.get "bintrayKey"
    publications = ['filters']

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'glide'          // or your repo name
        name = projectName      // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['MIT']
        vcsUrl = 'https://github.com/kdabir/glide.git'
    }
//    dryRun = true // whether to run this as dry-run, without deploying
}

release {
    tagTemplate = '$name-$version'
}

afterReleaseBuild.dependsOn bintrayUpload