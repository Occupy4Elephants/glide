plugins {
    id 'maven-publish'
    id "com.jfrog.bintray" version '1.4'
}

version = project.glideFiltersVersion
description = "Filters for glide apps"

dependencies {
    compile 'javax.servlet:servlet-api:2.5'
    compile "org.codehaus.groovy:groovy-all:${project.groovyVersion}"

    compile "com.google.appengine:appengine-api-1.0-sdk:$gaeVersion"
    compile "com.google.appengine:appengine-api-labs:$gaeVersion"
    compile "org.gaelyk:gaelyk:$gaelykVersion"

    testCompile "junit:junit:$junitVersion"
}


sourceSets {
    main.groovy.srcDirs = ['src']
    test.groovy.srcDirs = ['test']

    main.resources.srcDirs = test.resources.srcDirs = main.java.srcDirs = test.java.srcDirs = []
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}


// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = project.properties.get "bintrayUser"
    key = project.properties.get "bintrayKey"
    publications = ['mavenJava']

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'glide'          // or your repo name
        name = projectName      // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kdabir/glide.git'
    }
//    dryRun = true // whether to run this as dry-run, without deploying
}
